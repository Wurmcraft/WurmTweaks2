buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
plugins {
    id 'com.diffplug.gradle.spotless' version '3.4.0'
    id "com.github.johnrengelman.shadow" version "2.0.4"
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "${mod_version}"
group = "com.wurmcraft.wurmtweaks" 
archivesBaseName = "WurmTweaks"

sourceCompatibility = targetCompatibility = '1.8' 
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "${mc_version}-${forge_version}"
    runDir = "run"
    mappings = "${mcp_map}"
	replace "@version@", project.version
    replaceIn "Global.java"
    clientRunArgs += "--username=${username}"
}

repositories {
    maven { url 'https://maven.mcmoddev.com' }
    maven { url 'http://maven.blamejared.com'}
}

dependencies {
    compile group: 'com.github.stephenc.high-scale-lib', name: 'high-scale-lib', version: '1.1.4'
    compile "net.darkhax.gamestages:GameStages-${mc_version}:${gamestages_version}"
    compile "net.darkhax.orestages:OreStages-${mc_version}:${orestages_version}"
	compile "net.darkhax.bookshelf:Bookshelf-1.12.2:${bookshelf_version}"
    compile "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-${crafttweaker2_version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

spotless {
    java {
        removeUnusedImports()
        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

shadowJar {
    dependencies {
        exclude('dummy*')
        include(dependency('com.github.stephenc.high-scale-lib:high-scale-lib:1.1.4'))
    }
}

reobf {
    shadowJar {}
}

task buildAndPackage {
    dependsOn 'clean'
    dependsOn 'spotlessApply'
    dependsOn 'build'
    tasks.findByName('build').mustRunAfter 'clean'
}
